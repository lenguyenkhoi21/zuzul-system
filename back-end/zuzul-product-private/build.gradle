plugins {
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2021.0.0")
    set('keyCloakVersion', "15.1.1")
}

dependencies {
    // Lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // Config properties
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // AWS
    implementation platform('software.amazon.awssdk:bom:2.17.102')
    implementation 'software.amazon.awssdk:s3'

    // Hibernate Validator
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Ops
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Web-Servlet Stack
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // MongoDB
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Cloud Config
    implementation 'org.springframework.cloud:spring-cloud-starter'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'

    // Logging
    implementation 'net.logstash.logback:logstash-logback-encoder:6.0'

    // Tracing
    implementation 'org.springframework.cloud:spring-cloud-sleuth-zipkin'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'

    // Service Registration
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'


    // Keycloak
    implementation 'org.keycloak:keycloak-spring-boot-starter'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.keycloak.bom:keycloak-adapter-bom:${keyCloakVersion}"
    }
}

jar {
    archiveFileName = 'app.jar'
}

test {
    useJUnitPlatform()
}
